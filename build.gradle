plugins {
  id 'com.gradleup.shadow' version '8.3.5'
  id 'application'
  id 'groovy'
  id 'java'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url = "https://s3-eu-west-1.amazonaws.com/maven.seqera.io/releases" }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
  options.release.set(17)
  options.compilerArgs << '-parameters' 
}

test {
  useJUnitPlatform()
}

configurations {
  nextflowRuntime
  specImplementation.extendsFrom(nextflowRuntime)
}

dependencies {
  implementation 'io.nextflow:nf-lang:25.09.1-edge'
  implementation 'org.apache.groovy:groovy:4.0.28'
  implementation 'org.apache.groovy:groovy-json:4.0.28'
  implementation 'org.apache.groovy:groovy-yaml:4.0.28'
  implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.23.0'
  implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.23.0'

  // runtime dependencies for Nextflow scripts
  runtimeOnly 'org.apache.groovy:groovy-templates:4.0.28'
  runtimeOnly 'org.yaml:snakeyaml:2.2'

  // include Nextflow runtime at build-time to extract language definitions
  nextflowRuntime 'io.nextflow:nextflow:25.09.1-edge'
  nextflowRuntime 'io.nextflow:nf-amazon:3.3.0'
  nextflowRuntime 'io.nextflow:nf-azure:1.20.1'
  nextflowRuntime 'io.nextflow:nf-google:1.23.1'
  nextflowRuntime 'io.nextflow:nf-k8s:1.2.1'
  nextflowRuntime 'io.nextflow:nf-tower:1.16.0'
  nextflowRuntime 'io.nextflow:nf-wave:1.16.0'

  testImplementation ('org.objenesis:objenesis:3.4')
  testImplementation ('net.bytebuddy:byte-buddy:1.14.17')
  testImplementation ('org.spockframework:spock-core:2.3-groovy-4.0') { exclude group: 'org.apache.groovy' }
}

sourceSets {
  spec {
    groovy.srcDir 'src/spec/groovy'
    compileClasspath += configurations.nextflowRuntime
    runtimeClasspath += configurations.nextflowRuntime
  }
}

task buildSpec(type: JavaExec) {
  description = 'Build spec file of core definitions'
  group = 'build'

  dependsOn compileSpecGroovy
  classpath = sourceSets.spec.runtimeClasspath
  mainClass.set('nextflow.SpecWriter')
  args "$buildDir/generated/definitions.json"

  outputs.file("$buildDir/generated/definitions.json")
  outputs.cacheIf { true }

  doFirst {
    file("$buildDir/generated").mkdirs()
  }
}

processResources {
  dependsOn buildSpec
  from(buildSpec.outputs.files) {
    into 'spec'
  }
}

jar {
  dependsOn buildSpec
  from("$buildDir/generated") {
    include 'definitions.json'
    into 'spec'
  }
}

application {
  mainClass = 'nextflow.lsp.NextflowLanguageServer'
}

shadowJar {
  archiveVersion = ''
}
