plugins {
  id 'com.gradleup.shadow' version '8.3.5'
  id 'org.graalvm.buildtools.native' version '0.10.4'
  id 'application'
  id 'groovy'
  id 'java'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url = "https://s3-eu-west-1.amazonaws.com/maven.seqera.io/releases" }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
  options.release.set(17)
  options.compilerArgs << '-parameters' 
}

test {
  useJUnitPlatform()
}

configurations {
  nextflowRuntime
}

dependencies {
  implementation 'io.nextflow:nf-lang:25.10.0'
  implementation 'org.apache.groovy:groovy:4.0.28'
  implementation 'org.apache.groovy:groovy-json:4.0.28'
  implementation 'org.apache.groovy:groovy-yaml:4.0.28'
  implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.23.0'
  implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.23.0'

  // runtime dependencies for Nextflow scripts
  runtimeOnly 'org.apache.groovy:groovy-templates:4.0.28'
  runtimeOnly 'org.yaml:snakeyaml:2.2'

  // include Nextflow runtime at build-time to extract language definitions
  nextflowRuntime 'io.nextflow:nextflow:25.10.0'
  nextflowRuntime 'io.nextflow:nf-amazon:3.4.1'
  nextflowRuntime 'io.nextflow:nf-azure:1.20.2'
  nextflowRuntime 'io.nextflow:nf-google:1.23.3'
  nextflowRuntime 'io.nextflow:nf-k8s:1.2.2'
  nextflowRuntime 'io.nextflow:nf-tower:1.17.1'
  nextflowRuntime 'io.nextflow:nf-wave:1.16.1'

  testImplementation ('org.objenesis:objenesis:3.4')
  testImplementation ('net.bytebuddy:byte-buddy:1.14.17')
  testImplementation ('org.spockframework:spock-core:2.3-groovy-4.0') { exclude group: 'org.apache.groovy' }
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
  spec {
    groovy.srcDir 'src/spec/groovy'
    compileClasspath += configurations.nextflowRuntime
    runtimeClasspath += configurations.nextflowRuntime
  }
}

configurations {
  specImplementation.extendsFrom(nextflowRuntime)
}

task buildSpec(type: JavaExec) {
  description = 'Build spec file of core definitions'
  group = 'build'

  dependsOn compileSpecGroovy
  classpath = sourceSets.spec.runtimeClasspath
  mainClass.set('nextflow.SpecWriter')
  args "$buildDir/generated/definitions.json"

  outputs.file("$buildDir/generated/definitions.json")
  outputs.cacheIf { true }
}

processResources {
  dependsOn buildSpec
  from(buildSpec.outputs.files) {
    into 'spec'
  }
}

jar {
  dependsOn buildSpec
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from("$buildDir/generated") {
    include 'definitions.json'
    into 'spec'
  }
}

application {
  mainClass = 'nextflow.lsp.NextflowLanguageServer'
}

shadowJar {
  archiveVersion = ''
}

task generateNativeImageConfig(type: Exec) {
  description = 'Generate native-image configuration using tracing agent'
  group = 'native'

  dependsOn shadowJar

  def agentOutputDir = file("$buildDir/native-image-agent")
  def simulateScript = file("$projectDir/lsp-simulator.sh")

  inputs.file(shadowJar.archiveFile)
  inputs.file(simulateScript)
  outputs.dir(agentOutputDir)

  doFirst {
    agentOutputDir.mkdirs()
  }

  commandLine 'bash', '-c', """
    ${simulateScript.absolutePath} | java \\
      -agentlib:native-image-agent=config-output-dir=${agentOutputDir.absolutePath} \\
      -cp ${shadowJar.archiveFile.get().asFile.absolutePath} \\
      nextflow.lsp.NextflowLanguageServer
  """
}

tasks.named('nativeCompile') {
  dependsOn generateNativeImageConfig
}

graalvmNative {
  agent {
    defaultMode = 'standard'
    builtinCallerFilter = true
    builtinHeuristicFilter = true
    enableExperimentalPredefinedClasses = false
    enableExperimentalUnsafeAllocationTracing = false
    trackReflectionMetadata = true
    metadataCopy {
      inputTaskNames.add('run')
      outputDirectories.add("$buildDir/native-image-agent")
      mergeWithExisting = true
    }
  }
  binaries {
    main {
      imageName = 'nf-language-server'
      mainClass = 'nextflow.lsp.NextflowLanguageServer'
      buildArgs.addAll([
        '--no-fallback',
        '--enable-url-protocols=http,https',
        '-H:+ReportExceptionStackTraces',
        '--report-unsupported-elements-at-runtime',
        '--initialize-at-build-time=org.slf4j',
        '--initialize-at-build-time=org.apache.groovy',
        '--initialize-at-build-time=groovy',
        '--initialize-at-build-time=org.codehaus.groovy',
        '--initialize-at-build-time=groovyjarjarantlr4',
        '--initialize-at-build-time=java.beans',
        '--initialize-at-build-time=com.sun.beans',
        "-H:ConfigurationFileDirectories=$buildDir/native-image-agent"
      ])
    }
  }
  toolchainDetection = false
}
